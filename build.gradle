apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

version = '0.1'
group = 'com.tarasleskiv'
def artifact = 'gradle-liquibase-plugin'

configurations {
  deployment
  published.extendsFrom archives, signatures
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  groovy 'org.codehaus.groovy:groovy:1.8.6'
  compile gradleApi()
  deployment "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-7"
  compile 'org.liquibase:liquibase-core:2.0.3'
  runtime 'com.augusttechgroup:groovy-liquibase-dsl:0.7.3'
}

jar {
  baseName = artifact
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.0'
}


task sourceJar(type: Jar) {
  description = 'An archive of the source code for Maven Central'
  baseName = artifact
  classifier = 'sources'
  from sourceSets.main.groovy
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for Maven Central'
  baseName = artifact
  classifier = 'javadoc'
  from fileTree(groovydoc.destinationDir)
}

artifacts {
  archives groovydocJar, sourceJar
}

signing {
  sign configurations.archives
}


install {
  repositories.mavenInstaller {
    pom.project(pomConfiguration)
  }
}


uploadPublished {
  dependsOn << [ signArchives ]
  repositories.mavenDeployer {
    beforeDeployment { deployment ->
      signPom(deployment)
    }
    name = 'mavenCentralReleaseDeployer'
    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: project.mavenCentralUsername, password: project.mavenCentralPassword)
      releases(updatePolicy: 'always')
      snapshots(updatePolicy: 'never')
    }
    pom.project(pomConfiguration)
  }
}




//
// A single place to create the POM configuration, so it can be
// applied from the install and the upload tasks.
//
def getPomConfiguration() {
  return {
    name('Gradle Unity3d Plugin')
    description('A Gradle plugin for running Unity command line commands easier.')
    url('https://github.com/TarasOsiris/unity-gradle-plugin')
    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
    developers {
      developer {
        id('tlberglund')
        name('Taras Leskiv')
        email('leskiv.taras@gmail.com')
      }
    }
    scm {
      connection('scm:https://github.com/TarasOsiris/unity-gradle-plugin')
      developerConnection('git@github.com:TarasOsiris/unity-gradle-plugin.git')
      url('https://github.com/TarasOsiris/unity-gradle-plugin')
    }
  }
}
